package cepneuralink;

global sbnz.mrsandman.neuralinkapp.bus.EventBus eventBus;

import sbnz.mrsandman.neuralinkapp.model.events.alcohol.*;
import sbnz.mrsandman.neuralinkapp.model.events.heartrate.*;
import sbnz.mrsandman.neuralinkapp.model.events.temperature.*;
import sbnz.mrsandman.neuralinkapp.model.events.physicalactivity.*;
import sbnz.mrsandman.neuralinkapp.model.events.caffeine.*;
import sbnz.mrsandman.neuralinkapp.model.events.light.*;
import sbnz.mrsandman.neuralinkapp.model.events.*;
import sbnz.mrsandman.neuralinkapp.model.enums.*;
import sbnz.mrsandman.neuralinkapp.model.*;
import java.time.LocalTime;
import java.time.LocalDateTime;
import java.util.Date;
import java.time.Instant;
import java.time.ZoneId;

rule "Raised alcohol level 4 hours before sleep"
	no-loop
    when       
        SleepPhaseEvent(sleepPhase == SleepPhase.AWAKE)
        $u:User(optimalSleepTime != null)
		$al1: RaisedAlcoholLevelEvent() 
        Number(intValue >= 3) from accumulate(
        	$al2: RaisedAlcoholLevelEvent(
        		this != $al1,
        		this meets[4h] $al1
        	),
        	count($al2) 
        )
        $avg_alch: Double() from accumulate(
        	$al2: RaisedAlcoholLevelEvent(
        		$val: value,
        		this != $al1,
        		this meets[4h] $al1
        	),
        	average($val) 
        )
        not(
            AlcoholBeforeSleepEvent() 
            over window:time(4h)
        )
    then
    	System.out.println(LocalDateTime.ofInstant(Instant.ofEpochMilli(drools.getWorkingMemory().getSessionClock().getCurrentTime()), ZoneId.systemDefault()).toLocalTime());
    	System.out.println("Warning! Increased blood alcohol level detected less that 4 hours before optimal sleep time!");
        insert(new AlcoholBeforeSleepEvent($avg_alch, LocalDateTime.ofInstant(Instant.ofEpochMilli(drools.getWorkingMemory().getSessionClock().getCurrentTime()), ZoneId.systemDefault())));
end


rule "Intense physical activity 2 hours before optimal sleep time"
	no-loop
    when       
        SleepPhaseEvent(sleepPhase == SleepPhase.AWAKE)
        $u:User(optimalSleepTime != null)
		$temp: RaisedTemperatureEvent() 
        Number(intValue >= 3) from accumulate(
        	$t: RaisedTemperatureEvent(
        		this != $temp,
        		this meets[2h] $temp
        	),
        	count($t) 
        )
		$hr: HeartRateIncreasedEvent()
        Number(intValue >= 3) from accumulate(
        	$h: HeartRateIncreasedEvent(
        		this != $hr,
        		this meets[2h] $hr
        	),
        	count($h) 
        )
        $avg_temp: Double() from accumulate(
        	$t2: RaisedTemperatureEvent(
        		$val: value,
        		this != $temp,
        		this meets[2h] $temp
        	),
        	average($val) 
        )
        not(
            PhysicalActivityEvent()
            over window:time(10m)
        )
    then
    	System.out.println("Warning! Intense physical activity 2 hours before optimal sleep time!");
        insert(new PhysicalActivityEvent($avg_temp, LocalDateTime.ofInstant(Instant.ofEpochMilli(drools.getWorkingMemory().getSessionClock().getCurrentTime()), ZoneId.systemDefault())));
end

rule "Raised caffeine level 6 hours before sleep"
	no-loop
    when       
        SleepPhaseEvent(sleepPhase == SleepPhase.AWAKE)
        $u:User(optimalSleepTime != null)
		$cf1: RaisedCaffeineLevelEvent() 
        Number(intValue >= 3) from accumulate(
        	$cf2: RaisedCaffeineLevelEvent(
        		this != $cf1,
        		this meets[10m] $cf1
        	),
        	count($cf2) 
        )
        $avg_caff: Double() from accumulate(
        	$cf2: RaisedCaffeineLevelEvent(
        		$val: value,
        		this != $cf1,
        		this meets[2h] $cf1
        	),
        	average($val) 
        )
        not(
            CaffeineBeforeSleepEvent() 
            over window:time(10m)
        )
    then
    	System.out.println("Warning! Increased blood caffeine level detected less that 6 hours before optimal sleep time!");
        insert(new CaffeineBeforeSleepEvent($avg_caff, LocalDateTime.ofInstant(Instant.ofEpochMilli(drools.getWorkingMemory().getSessionClock().getCurrentTime()), ZoneId.systemDefault())));
end

rule "Raised light level 2 hours before sleep"
	no-loop
    when       
        SleepPhaseEvent(sleepPhase == SleepPhase.AWAKE)
        $u:User(optimalSleepTime != null)
		$e1: BrightLightEvent() 
        Number(intValue >= 3) from accumulate(
        	$e2: BrightLightEvent(
        		this != $e1,
        		this meets[10m] $e1
        	),
        	count($e2) 
        )
        $avg_lux: Double() from accumulate(
        	$e2: BrightLightEvent(
        		$val: value,
        		this != $e1,
        		this meets[2h] $e1
        	),
        	average($val) 
        )
        not(
            BrightLightBeforeSleepEvent() 
            over window:time(10m)
        )
    then
    	System.out.println("Warning! Increased bright light level detected less that 2 hours before optimal sleep time!");
        insert(new BrightLightBeforeSleepEvent($avg_lux, LocalDateTime.ofInstant(Instant.ofEpochMilli(drools.getWorkingMemory().getSessionClock().getCurrentTime()), ZoneId.systemDefault())));
end
